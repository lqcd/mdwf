(module qa0-new
        mzscheme
   (require "common.ss")
   (require "be-c99-new.ss")
   (require "be-bgl-xlc-new.ss")
   (require "cheader.ss")
   (require "backend.ss")
   (require "parser.ss")
   (require "cfold.ss")
   (require "q2complex.ss")
   (require "cx2dh.ss")
   (require "qa0print.ss")

   (define *version* "$Id$")
   (define (do-help arg*)
     (for-each (lambda (cdf) (printf "\t~a\t~a~%" (car cdf) (cadr cdf)))
	       cmd*))
   (define (la-compilier arg* machine front back)
     (if (not  (= (vector-length arg*) 4))
	 (error 'qa0 "bad arguments"))
     (let* ([d/f (vector-ref arg* 1)]
	    [output (vector-ref arg* 2)]
	    [input (vector-ref arg* 3)]
	    [d/f (cond
		  [(string=? d/f "double") 'double]
		  [(string=? d/f "float") 'float]
		  [else (error 'qa "Unexpected value for d/f: ~a" d/f)])])
       (if (file-exists? output) (delete-file output))
       (with-output-to-file output
	 (lambda ()
	   (printf "/* automagically generated by qa0 ~a */~%" *version*)
	   (front input machine d/f back)))))
   (define (do-header arg*)
     (la-compilier arg* machine-c99-32 qa0-compile c-header))
   (define (do-c99 arg*)
     (la-compilier arg* machine-c99-32 qa0-compile emit-back-end))
   (define (do-bgl/xlc arg*)
     (la-compilier arg* machine-bgl/xlc qa0-compile emit-back-end))
   (define (print-tree/env ast env) (print-tree ast))
   (define (do-version arg*)
     (printf "qa0: ~a~%" *version*))
   (define (do-macro arg*)
     (la-compilier arg* machine-c99-32 qa0-macro print-tree/env))
   (define (do-expand arg*)
     (la-compilier arg* machine-c99-32 qa0-expand print-tree/env))
   (define (do-dhexpand arg*)
     (la-compilier arg* machine-bgl/xlc qa0-dhexpand print-tree/env))
   (define (qa0-macro name machine d/f back-end)
     (let-values* ([ast (parse-qa0-file name)]
                   [(ast env) (fold-constants/env ast machine d/f)])
        (back-end ast env)))
   (define (qa0-expand name machine d/f back-end)
     (qa0-macro name machine d/f
		(lambda (ast env)
		  (let-values* ([(ast e-x) (qcd->complex ast env)])
		    (back-end ast env)))))
   (define (qa0-dhexpand name machine d/f back-end)
     (qa0-expand name machine d/f
		 (lambda (ast env)
		   (let ([ast (complex->double-hummer ast)])
		     (back-end ast env)))))
   (define (qa0-compile name machine d/f back-end)
     (qa0-expand name machine d/f
		 (lambda (ast env)
		   (let-values* ([(ast env) (complex->back-end ast env)])
                     (back-end ast env)))))
   (define cmd*
     `(("help"    "             print available commands"        ,do-help)
       ("header"  "d/f out in   build .h file for double/float"  ,do-header)
       ("c99"     "d/f out in   build .c file for double/float"  ,do-c99)
       ("bgl/xlc" "d/f out in   build .c file for XLC on BG/L"   ,do-bgl/xlc)
       ("macro"   "d/f out in   do constant folding only"        ,do-macro)
       ("expand"  "d/f out in   convert to complex only"         ,do-expand)
       ("dhexpand" "d/f out in   convert to DH only"             ,do-dhexpand)
       ("version" "             print qa0 version"               ,do-version)))

   (define (dispatch name arg*)
     (let loop ([cmd* cmd*])
       (cond
	[(null? cmd*) (printf "qa0: Unknown command ~s. Try saying qa0 help~%"
			      name)]
	[(string=? name (caar cmd*)) ((caddar cmd*) arg*)]
	[else (loop (cdr cmd*))])))

   (let ([arg* (current-command-line-arguments)])
     (if (zero? (vector-length arg*))
         (dispatch "help" '#("help"))
         (dispatch (vector-ref arg* 0) arg*))))
